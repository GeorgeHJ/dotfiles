#! /usr/bin/env zsh
# vim: foldmethod=marker

# Direnv{{{
[[ -z $TMUX ]] || typeset -g POWERLEVEL9K_INSTANT_PROMPT=quiet
command -V direnv >/dev/null 2>&1 && eval "$(direnv hook zsh)"
# }}}

# {{{ P10k Instant Prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
	source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
# }}}

#{{{ History File
XDG_STATE_HOME=${XDG_STATE_HOME:-$HOME/.local/state}
HISTFILE=$XDG_STATE_HOME/zsh/history
HISTSIZE=10000
SAVEHIST=10000
HISTDUP=erase
if [[ ! -f "$HISTFILE" ]];then
	mkdir -p "$XDG_STATE_HOME/zsh" && touch "$HISTFILE"
fi
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups
#}}}

#{{{ Fallback PS1
autoload -U colors && colors
PS1="%B[%{$fg[green]%}%n%{$fg[green]%}%{$reset_color%}%B@%{$fg[green]%}%M %{$reset_color%}%B%~]%{$reset_color%}%#%(?..%{$fg[red]%} %?)%b "
[[ -n $CONTAINER_ID ]] && PS1="$fg[magenta]◆$reset_color ${PS1}"
#}}}

#{{{ Dircolors
# Use dircolors to set LS_COLOR
[[ -f "$HOME/.dircolors" ]] &&
	eval "$(dircolors "$HOME/.dircolors")" ||
	[[ -f "$HOME/.config/dir_colors/dir_colors" ]] &&
	eval "$(dircolors "$HOME/.config/dir_colors/dir_colors")"
#}}}

#{{{ VS Code Integration
[[ "$TERM_PROGRAM" == "vscode" ]] && source "/usr/lib/code/out/vs/workbench/contrib/terminal/browser/media/shellIntegration-rc.zsh"
#}}}

#{{{ Kitty Integration
[[ "$TERM" =~ ".*kitty" ]] && source "/usr/lib/kitty/shell-integration/zsh/kitty.zsh"
#}}}

# Shell options{{{
unsetopt nomatch
setopt interactivecomments

# Initialise arrays for hook functions
typeset -a precmd_functions preexec_functions
#}}}

# {{{ Completion Options
# Include hidden files in autocomplete:
_comp_options+=(globdots)
# Add personal completions and functions directories to fpath
fpath=($ZDOTDIR/completions $ZDOTDIR/functions $fpath)
# Add (Nix) Home Manager if there
if [[ -d $HOME/.nix-profile/share/zsh/site-functions ]]; then
		fpath=("$HOME/.nix-profile/share/zsh/site-functions" $fpath)
fi
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion::*:-tilde-:*:*' group-order named-directories users

# Menu Completion Listing colours
zstyle ':completion:*:default'		list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ""

## Contexts
zstyle ':completion:*:commands'	\
	list-colors 'ma='$color[green]';'$color[bold]';'$color[reverse]':=*='$color[green]''
zstyle ':completion:*:builtins'	\
	list-colors 'ma='$color[green]';'$color[bold]';'$color[reverse]':=*='$color[green]''
zstyle ':completion:*:aliases' \
	list-colors 'ma='$color[green]';'$color[bold]';'$color[reverse]':=*='$color[green]''
zstyle ':completion:*:functions' \
	list-colors 'ma='$color[green]';'$color[bold]';'$color[reverse]':=*='$color[green]''
zstyle ':completion:*:executable' \
	list-colors 'ma='$color[green]';'$color[bold]';'$color[reverse]':=*='$color[green]''
zstyle ':completion:*:options' \
	list-colors 'ma='$color[default]';'$color[bold]';'$color[reverse]':=*='$color[default]''
zstyle ':completion:*:arguments' \
	list-colors 'ma='$color[default]';'$color[bold]';'$color[reverse]':=*='$color[default]''
zstyle ':completion:*:reserved-words' \
	list-colors 'ma='$color[yellow]';'$color[bold]';'$color[reverse]':=*='$color[yellow]''
zstyle ':completion:*:named-directories' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]':=*='$color[blue]''
zstyle ':completion:*:packages' \
	list-colors 'ma=95;'$color[bold]';'$color[reverse]':=*=95'
zstyle ':completion:*:parameters' \
	list-colors 'ma=91;'$color[bold]';'$color[reverse]':=*=91'
zstyle ':completion:*:manuals' \
	list-colors 'ma='$color[default]';'$color[bold]';'$color[reverse]':=*='$color[default]''

## Tags
zstyle ':completion::*:*::local-directories' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]''
zstyle ':completion::*:*::path-directories' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]''
zstyle ':completion::*:*::directories' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]''

## Programs
zstyle ':completion:*:*:rmdir:*' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]''
zstyle ':completion:*:*:mkdir:*' \
	list-colors 'ma='$color[blue]';'$color[bold]';'$color[reverse]''

zmodload zsh/complist
# }}}

#{{{ Extra functions
autoload diary wik wake
# }}}

#{{{ compinstall and compinit options
zstyle :compinstall filename "$XDG_CONFIG_HOME/zsh/zshrc"
zstyle ':completion:*' rehash true
zstyle ':completion::complete:*' use-cache on
zstyle ':completion:*:descriptions' format $'%B%{\e[10;39m%}[%d]%b%{\e[0m%}'
zstyle ':completion:*:messages' format '%U%d%u'
zstyle ':completion:*:warnings' format "%B%{$fg[red]%}No match for:%b%{$reset_color%} %d"
zstyle ':completion:*' group-name ''

# Execute code in the background to not affect the current session
{
	# Compile zcompdump, if modified, to increase startup speed.
	zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
	if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
		zcompile "$zcompdump"
	fi
} &!

autoload -Uz compinit
compinit
#}}} End of lines added by compinstall

#{{{ Bash-style help
autoload run-help
[[ -n $aliases[run-help] ]] && unalias run-help
alias help=run-help
# }}}

#{{{ Named Directories
declare -A hash_dirs
[[ -d $HOME/.local/bin ]] && hash_dirs[bin]="$HOME/.local/bin"
[[ -d "$XDG_DOCUMENTS_DIR/University/Birkbeck" ]] && hash_dirs[birkbeck]="$XDG_DOCUMENTS_DIR/University/Birkbeck"
[[ -d $XDG_CONFIG_HOME ]] && hash_dirs[config]="$XDG_CONFIG_HOME"
[[ -d "$XDG_DOCUMENTS_DIR" ]] && hash_dirs[doc]="$XDG_DOCUMENTS_DIR"
[[ -d "$XDG_DOWNLOAD_DIR" ]] && hash_dirs[downloads]="$XDG_DOWNLOAD_DIR"
[[ -d "$HOME/.local/src/Repos/Github" ]] && hash_dirs[github]="$HOME/.local/src/Repos/Github"
[[ -d "$GOPATH" ]] && hash_dirs[go]="$GOPATH"
[[ -d "$XDG_MUSIC_DIR" ]] && hash_dirs[music]="$XDG_MUSIC_DIR"
[[ -d "$XDG_CONFIG_HOME/nvim" ]] && hash_dirs[nvim]="$XDG_CONFIG_HOME/nvim"
[[ -d "$XDG_DOCUMENTS_DIR/Notes" ]] && hash_dirs[notes]="$XDG_DOCUMENTS_DIR/Notes"
[[ -d "$HOME/.local/src/Repos" ]] && hash_dirs[repos]="$HOME/.local/src/Repos"
[[ -d "$HOME/.local/src/" ]] && hash_dirs[src]="$HOME/.local/src/"
[[ -d "$XDG_CONFIG_HOME/vim" ]] && hash_dirs[vim]="$XDG_CONFIG_HOME/vim"
[[ -d "$XDG_DATA_HOME/whipper/rips" ]] && hash_dirs[whipper_rips]="$XDG_DATA_HOME/whipper/rips"
[[ -d "$XDG_CONFIG_HOME/zsh" ]] && hash_dirs[zsh]="$XDG_CONFIG_HOME/zsh"
[[ -d "$XDG_DATA_HOME" ]] && hash_dirs[data]="$XDG_DATA_HOME"

declare -A desc_dirs
desc_dirs[bin]='Executable scripts'
desc_dirs[birkbeck]='Birkbeck University'
desc_dirs[config]='Configuration directory'
desc_dirs[data]='Application data directory'
desc_dirs[doc]='Documents'
desc_dirs[downloads]='Downloads'
desc_dirs[github]='Github'
desc_dirs[go]='Go Language Workspace'
desc_dirs[music]='Music'
desc_dirs[nvim]='Neovim configurations'
desc_dirs[notes]='Notes'
desc_dirs[repos]='Repositories'
desc_dirs[src]='Code'
desc_dirs[vim]='Vim configurations'
desc_dirs[whipper_rips]='Whipper rips'
desc_dirs[zsh]='Zsh directory'

# Loop over the associative array to build the directory hashes
for key value in ${(kv)hash_dirs};
do
	if [[ -L "$value" ]];then
		hash -d "$key"="$(realpath "$value")"
	elif [[ -d "$value" ]]; then
		hash -d "$key"="$value"
	fi
done
unset hash_dirs

autoload fzf_named_dirs
zle -N fzf_named_dirs
bindkey '^f' fzf_named_dirs

# The standard _tilde completion function with users removed named directories only
_tilde_no_users () {
	[[ -n "$compstate[quote]" ]] && return 1
	local expl suf ret=1
	if [[ "$SUFFIX" = */* ]]; then
		ISUFFIX="/${SUFFIX#*/}$ISUFFIX"
		SUFFIX="${SUFFIX%%/*}"
		suf=(-S '')
	else
		suf=(-qS/)
	fi
	_tags named-directories directory-stack
	while _tags; do
		_requested named-directories expl 'named directory' compadd "$suf[@]" "$@" -k nameddirs && ret=0
		_requested directory-stack && _directory_stack "$suf[@]" && ret=0
		(( ret )) || return 0
	done
	return ret
}
compdef _tilde_no_users -tilde-

#}}}

#{{{ Vi Keybindings

# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}":
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

bindkey -v # Use Vi Keys
export KEYTIMEOUT=1 # Suggested for Vi mode

# Insert Mode
bindkey "$key[Backspace]" backward-delete-char
bindkey '^?' backward-delete-char
bindkey -M viins '^H' backward-delete-char
bindkey "$key[Delete]" delete-char
bindkey '^[[H' beginning-of-line
bindkey "\E[1~" beginning-of-line
bindkey '^[[F' end-of-line
bindkey "\E[4~" end-of-line

# Normal Mode
bindkey -a "$key[Delete]" vi-delete-char
bindkey -a '^[[H'  beginning-of-line
bindkey -a "^[[1~" beginning-of-line
bindkey -a '^[[F' end-of-line
bindkey -a "^[[4~" end-of-line

# Insert Mode bindings
bindkey '^p' up-history
bindkey '^n' down-history
bindkey '^l' clear-screen
bindkey '^k' kill-line

# Menu completion bindings
## use the vi navigation keys in menu completion
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect "$key[Shift-Tab]" reverse-menu-complete
bindkey -M menuselect '^o' accept-and-infer-next-history
bindkey -M menuselect '/' accept-and-infer-next-history
bindkey -M menuselect ' ' accept-and-hold
# Edit line in $EDITOR with ctrl-v:
autoload edit-command-line; zle -N edit-command-line
bindkey -a '^v' edit-command-line

# Cycle through history based on characters already typed on the line
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey -a "k" up-line-or-beginning-search
bindkey -a "j" down-line-or-beginning-search
bindkey -a "^[[A" up-line-or-beginning-search
bindkey -a "^[[OA" up-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search
bindkey "^[[OA" up-line-or-beginning-search
bindkey -a "^[[B"  down-line-or-beginning-search
bindkey -a "^[[OB" down-line-or-beginning-search
bindkey "^[[B"  down-line-or-beginning-search
bindkey "^[[OB" down-line-or-beginning-search
#}}}

#{{{ Vim text objects
# ci", ci', ci`, di", etc
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
	for c in {a,i}{\',\",\`}; do
		bindkey -M $m $c select-quoted
	done
done

# ci{, ci(, ci<, di{, etc
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
	for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
		bindkey -M $m $c select-bracketed
	done
done
#}}}

#{{{ Widgets and bindings
vicd_wrapper(){BUFFER="vicd $PWD"; zle accept-line; }
zle -N vicd_wrapper
bindkey -a '^o' vicd_wrapper
# Fzf Manpages
zle -N fman
bindkey -a 'm' fman
# Mpq wrapper
if command -v mpq >/dev/null 2>&1;then
	mpq_wrapper(){BUFFER="mpq"; zle accept-line; }
	zle -N mpq_wrapper
	bindkey -a '^i' mpq_wrapper
fi
#}}}

# {{{ Luke Smith — cursor settings
# https://github.com/LukeSmithxyz/voidrice/blob/master/.config/zsh/.zshrc
# Now autoloaded from my fpath
[[ -n $functions[zle-line-init] ]] && unfunction zle-line-init
[[ -n $functions[zle-keymap-select] ]] && unfunction zle-keymap-select
autoload zle-line-init; zle -N zle-line-init
autoload zle-line-blockcursor; zle -N zle-line-blockcursor

# Add it to the array of preexec functions
preexec_functions+=(zle-line-blockcursor)

autoload zle-keymap-select; zle -N zle-keymap-select
# }}}

#{{{ Source extra files

#Global alias file
[[ -f "$HOME/.config/shells/alias" ]] && source "$HOME/.config/shells/alias"
[[ -f "$HOME/.config/shells/functions" ]] && source "$HOME/.config/shells/functions"
# Pacman command not found hook
if
	[[ -f '/usr/share/doc/pkgfile/command-not-found.zsh' ]]
then
	source /usr/share/doc/pkgfile/command-not-found.zsh
elif
	[[ -a '/etc/zsh_command_not_found' ]]
then
	source /etc/zsh_command_not_found
fi

if
	[[ "command -v broot" ]]
then
	autoload br
fi
#}}}

# {{{ FZF Options
# Use fzf
unamea=$(uname -a)
if [[ $unamea == *Android* ]] ;then
	[[ -f "$PREFIX/share/fzf/completion.zsh" ]] && source "$PREFIX/share/fzf/completion.zsh"
	[[ -f "$PREFIX/share/fzf/key-bindings.zsh" ]] && source "$PREFIX/share/fzf/key-bindings.zsh"
else
	[[ -f "/usr/share/fzf/completion.zsh" ]] && source /usr/share/fzf/completion.zsh
	[[ -f "/usr/share/fzf/key-bindings.zsh" ]] && source /usr/share/fzf/key-bindings.zsh
	[[ -f "/usr/share/doc/fzf/examples/completion.zsh" ]] && source /usr/share/doc/fzf/examples/completion.zsh
	[[ -f "/usr/share/doc/fzf/examples/key-bindings.zsh" ]] && source /usr/share/doc/fzf/examples/key-bindings.zsh
fi
# Preview alt-c directories with tree
export FZF_ALT_C_OPTS="--preview 'tree -C {} | head -100'"
# }}}

#{{{ Terminals: tmux/xterm, title options and little fixes

autoload tmux_title_restore tmux_title_set xterm_window_title

case $TERM in
	screen*|tmux*)
		precmd_functions+=(tmux_title_restore)
		preexec_functions+=(tmux_title_set)
		;;
	xterm*|wezterm)
		precmd_functions+=(xterm_window_title)
		;;
esac
#}}}

#{{{ Autosuggestions Settings
# Settings for Autosuggestions
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_USE_ASYNC=1
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#586e75,bold,underline"
bindkey '^ ' autosuggest-accept
bindkey 'M-w' forward-word
#}}}

# {{{ Plugins
if [[ -f /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh ]];then
	source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
elif [[ -f "$HOME/.local/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh" ]];then
	source "$HOME/.local/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh"
fi
if [[ -f /usr/share/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh ]]; then
	source /usr/share/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
elif [[ -f "/home/george/.local/share/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh" ]];then
	source "/home/george/.local/share/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"
elif [[ -f "$HOME/.nix-profile/share/zsh/site-functions/fast-syntax-highlighting.plugin.zsh" ]];then
		source "$HOME/.nix-profile/share/zsh/site-functions/fast-syntax-highlighting.plugin.zsh"
fi
if [[ $(tput colors) -ge "256" ]];then
	autoload powerlevel9k; powerlevel9k
	if [[ -f /usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme ]];then
		source /usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme
	elif [[ -f "$HOME/.local/share/zsh/themes/powerlevel10k/powerlevel10k.zsh-theme" ]];then
		source "$HOME/.local/share/zsh/themes/powerlevel10k/powerlevel10k.zsh-theme"
	fi
fi
# }}}
